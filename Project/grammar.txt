file : EMPTY
     | include file
     | program

include : INCLUDE STRING

program : definition
        | definition program

definition : variableDefinition
           | functionDefinition

variableDefinition : ID EQUAL expr SEMI

functionDefinition : FUNCTION ID OPAREN optParamList CPAREN block

optParamList : EMPTY
             | paramList

paramList : ID
          | ID COMMA paramList

optExprList : EMPTY
           | exprList

exprList : expr
         | expr COMMA exprList

expr : primary
     | primary operator expr

primary : idDef
        | STRING
        | INT
        | NOT primary
        | OPAREN expr CPAREN
        | lambda
        | functionDefinition
        | OBRACKET optExprList CBRACKET

idDef : ID
      | ID OPAREN optExprList CPAREN
      | ID OBRACKET expr CBRACKET

operator : EQUAL
         | NOTEQUAL
         | GREATER
         | LESS
         | GREATEREQUAL
         | LESSEQUAL
         | PLUS
         | MINUS
         | MULTIPLY
         | DIVIDE
         | POWER
         | AND
         | OR
         | EQUAL

block : OBRACE optStatementList CBRACE

optStatementList : EMPTY
                 | statementList

statementList : statement
              | statement statementList

statement : variableDefinition
          | functionDefinition
          | expr SEMI
          | whileLoop
          | ifStatement
          | RETURN expr SEMI

whileLoop : WHILE OPAREN expr CPAREN block

ifStatement : IF OPAREN expr CPAREN block optElseStatement

optElseStatement : EMPTY
                 | elseStatement

elseStatement : ELSE block
              | ELSE ifStatement

lambda : LAMBDA OPAREN optParamList CPAREN block